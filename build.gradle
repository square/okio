buildscript {
  ext.versions = [
      'kotlin': '1.2.60',
      'kotlinNative': '0.8.2',
      'jmhPlugin': '0.4.5',
      'animalSnifferPlugin': '1.4.3',
      'dokka': '0.9.16',
      'jmh': '1.20',
      'animalSniffer': '1.16',
      'junit': '4.12',
      'assertj': '1.7.0',
      'shadowPlugin': '2.0.2',
      'nodePlugin': '1.2.0',
      'mavenPublishPlugin': '0.4.0',
      'ktlintPlugin': '5.1.0',
  ]

  ext.deps = [
      'kotlin': [
          'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
          'stdLib': [
              'common': "org.jetbrains.kotlin:kotlin-stdlib-common",
              'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
              'jdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7",
              'jdk6': "org.jetbrains.kotlin:kotlin-stdlib",
              'js': "org.jetbrains.kotlin:kotlin-stdlib-js",
          ],
          'test': [
              'common': "org.jetbrains.kotlin:kotlin-test-common",
              'annotations': "org.jetbrains.kotlin:kotlin-test-annotations-common",
              'jdk': "org.jetbrains.kotlin:kotlin-test-junit",
              'js': "org.jetbrains.kotlin:kotlin-test-js",
          ],
          'native': [
              'gradlePlugin': "org.jetbrains.kotlin:kotlin-native-gradle-plugin:${versions.kotlinNative}",
          ]
      ],
      'jmh': [
          'gradlePlugin': "me.champeau.gradle:jmh-gradle-plugin:${versions.jmhPlugin}",
          'core': "org.openjdk.jmh:jmh-core:${versions.jmh}",
          'generator': "org.openjdk.jmh:jmh-generator-annprocess:${versions.jmh}",
      ],
      'animalSniffer': [
          'gradlePlugin': "ru.vyarus:gradle-animalsniffer-plugin:${versions.animalSnifferPlugin}",
          'annotations': "org.codehaus.mojo:animal-sniffer-annotations:${versions.animalSniffer}",
      ],
      'japicmp': 'me.champeau.gradle:japicmp-gradle-plugin:0.2.6',
      'dokka': "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}",
      'shadow': "com.github.jengelman.gradle.plugins:shadow:${versions.shadowPlugin}",
      'node': "com.moowork.gradle:gradle-node-plugin:${versions.nodePlugin}",
      'mavenPublish': "com.vanniktech:gradle-maven-publish-plugin:${versions.mavenPublishPlugin}",
      'ktlint': "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:${versions.ktlintPlugin}",
      'test': [
          'junit': "junit:junit:${versions.junit}",
          'assertj': "org.assertj:assertj-core:${versions.assertj}",
      ]
  ]

  dependencies {
    classpath deps.kotlin.gradlePlugin
    classpath deps.kotlin.native.gradlePlugin
    classpath deps.animalSniffer.gradlePlugin
    classpath deps.japicmp
    classpath deps.dokka
    classpath deps.shadow
    classpath deps.jmh.gradlePlugin
    classpath deps.node
    classpath deps.mavenPublish
    classpath deps.ktlint
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

allprojects {
  group = GROUP
  version = VERSION_NAME
}

subprojects {
  repositories {
    mavenCentral()
    jcenter()
  }

  apply plugin: "org.jlleitschuh.gradle.ktlint"
}
