apply plugin: 'java-library'
apply plugin: 'ru.vyarus.animalsniffer'

kotlin.targets.matching { it.platformType.name == 'jvm' }.all { target ->
  target.project.sourceCompatibility = JavaVersion.VERSION_1_7
  target.project.targetCompatibility = JavaVersion.VERSION_1_7
  
  tasks['jvmJar'].configure { t ->
    // the bnd task convention modifies this jar task accordingly
    def bndConvention = bndBundleTaskConventionClass.newInstance(t);
    // modify these lines for MANIFEST.MF properties or for specific bnd instructions
    bndConvention.bnd = '''
    Export-Package: okio
    Automatic-Module-Name: okio
    Bundle-SymbolicName: com.squareup.okio
    '''
    // call the convention when the task has finished to modify the jar to contain OSGi metadata
    t.doLast {
      bndConvention.buildBundle()
    }
  }

  target.project.animalsniffer {
    sourceSets = [target.project.sourceSets.main]
  }
  
  target.project.dependencies {
    signature 'net.sf.androidscents.signature:android-api-level-15:4.0.3_r5@signature'
    signature 'org.codehaus.mojo.signature:java17:1.0@signature'
  }
}
