apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
  // jvmWithJava is getting deprecated but it's not clear what the replacement is:
  // https://youtrack.jetbrains.com/issue/KT-26256 
  targetFromPreset(presets.jvmWithJava, 'jvm')
  js() {
    configure([compilations.main, compilations.test]) {
      tasks.getByName(compileKotlinTaskName).kotlinOptions {
        moduleKind = "commonjs"
      }
    }
  }
  iosX64() {
    binaries {
      framework()
    }
  }
  iosArm64() {
    binaries {
      framework()
    }
  }
  // Required to generate tests tasks: https://youtrack.jetbrains.com/issue/KT-26547
  linuxX64()
  macosX64()
  mingwX64('winX64')
  sourceSets {
    commonMain {
      dependencies {
        api deps.kotlin.stdLib.common
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.kotlin.test.annotations
      }
    }
    jvmMain {
      dependencies {
        api deps.kotlin.stdLib.jdk6
        compileOnly deps.animalSniffer.annotations
      }
    }
    jvmTest {
      dependencies {
        implementation deps.test.junit
        implementation deps.test.assertj
        implementation deps.kotlin.test.jdk
      }
    }
    test {
      // Not required, but helps IntelliJ configure inter-project dependencies:
      // https://youtrack.jetbrains.com/issue/KT-31179
      dependsOn jvmTest
    }
    jsMain {
      dependencies {
        api deps.kotlin.stdLib.js
      }
    }
    jsTest {
      dependencies {
        implementation deps.kotlin.test.js
      }
    }
    nativeMain {
      dependsOn commonMain
    }
    nativeTest {
      dependsOn commonTest
    }

    configure([iosX64Main, iosArm64Main, linuxX64Main, macosX64Main, winX64Main]) {
      dependsOn nativeMain
    }
    configure([iosX64Test, iosArm64Test, linuxX64Test, macosX64Test, winX64Test]) {
      dependsOn nativeTest
    }
  }
}

// TODO(egorand): Remove when https://github.com/srs/gradle-node-plugin/issues/301 is fixed
repositories.whenObjectAdded {
  if (this instanceof IvyArtifactRepository) {
    metadataSources {
      artifact()
    }
  }
}

apply from: 'js/build.gradle'
apply from: 'jvm/jvm.gradle'
apply from: "$rootDir/gradle/gradle-mvn-mpp-push.gradle"
